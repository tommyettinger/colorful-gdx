[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

eclipse.project.name = project.name

sourceSets.test.resources.srcDirs = [ file('src/test/resources').path ]

javadoc.destinationDir = file('../docs/colorful/apidocs')
javadoc.options.source("17")
((StandardJavadocDocletOptions)javadoc.options).linkSource(true)
((StandardJavadocDocletOptions)javadoc.options).noTimestamp(true)
((StandardJavadocDocletOptions)javadoc.options).use(true)
((StandardJavadocDocletOptions)javadoc.options).bottom("Copyright &#169; Tommy Ettinger 2020-2025. All rights reserved. Are you a copyright lawyer? If so, I want you to think, are you actually helping people, or just wasting their time? Maybe do some pro bono work for a cause that matters?")

compileJava {
    sourceCompatibility = 7
    targetCompatibility = 7
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(7)
    }
}

compileTestJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

dependencies {
    api "com.badlogicgames.gdx:gdx:$gdxVersion"

    testImplementation "com.github.yellowstonegames.squidsquad:squidcore:$squidSquadVersion"
    testImplementation "com.github.yellowstonegames.squidsquad:squidgrid:$squidSquadVersion"
    testImplementation "com.github.yellowstonegames.squidsquad:squidplace:$squidSquadVersion"
    testImplementation "com.github.yellowstonegames.squidsquad:squidpath:$squidSquadVersion"
    testImplementation "com.github.yellowstonegames.squidsquad:squidtext:$squidSquadVersion"
    testImplementation "com.github.yellowstonegames.squidsquad:squidsmooth:$squidSquadVersion"

    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    testImplementation "com.github.tommyettinger:anim8-gdx:$anim8Version"
    testImplementation "junit:junit:4.13.2"
}

import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
}

mavenPlainJavadocJar.archiveBaseName.set(project.name)

jar {
    archiveBaseName.set(project.name)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}
