[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'

eclipse.project.name = project.name

sourceSets.test.resources.srcDirs = [ file('src/test/resources').path ]

javadoc.destinationDir = file('../docs/colorful/apidocs')
((StandardJavadocDocletOptions)javadoc.options).linkSource(true)
((StandardJavadocDocletOptions)javadoc.options).noTimestamp(true)
((StandardJavadocDocletOptions)javadoc.options).use(true)
((StandardJavadocDocletOptions)javadoc.options).bottom("Copyright &#169; Tommy Ettinger 2020-2025. All rights reserved.")

compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

compileTestJava {
    sourceCompatibility = 8
    targetCompatibility = 8
    if (JavaVersion.current().isJava9Compatible()) {
        options.release.set(8)
    }
}

dependencies {
    api "com.badlogicgames.gdx:gdx:$gdxVersion"
    testImplementation "com.badlogicgames.gdx:gdx:1.14.0"

    testImplementation "com.squidpony:squidcore:$squidSquadVersion"
    testImplementation "com.squidpony:squidgrid:$squidSquadVersion"
    testImplementation "com.squidpony:squidplace:$squidSquadVersion"
    testImplementation "com.squidpony:squidpath:$squidSquadVersion"
    testImplementation "com.squidpony:squidtext:$squidSquadVersion"
    testImplementation "com.squidpony:squidsmooth:$squidSquadVersion"

    testImplementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:1.14.0"
    testImplementation "com.badlogicgames.gdx:gdx-platform:1.14.0:natives-desktop"
    testImplementation "com.github.tommyettinger:anim8-gdx:$anim8Version"
    testImplementation "junit:junit:4.13.2"
}

import com.vanniktech.maven.publish.JavaLibrary
import com.vanniktech.maven.publish.JavadocJar

mavenPublishing {
    configure(new JavaLibrary(new JavadocJar.Javadoc(), true))
}

plainJavadocJar.archiveBaseName.set(project.name)

jar {
    archiveBaseName.set(project.name)
    manifest {
        attributes 'Implementation-Title': projectName, 'Implementation-Version': archiveVersion
    }
}
